:: Copyright (c) Microsoft Corporation.
:: Licensed under the MIT License.

:: This is a batch file to run common actions.
:: It can format the code, check the code, run the tests,
:: build the package, create a virtual environment, and clean up.
:: To avoid having to type `./make` all the time,
:: use `set-alias make ".\make.bat"` in PowerShell.

@echo off
if "%~1"=="" goto help

if /I "%~1"=="format" goto format
if /I "%~1"=="check" goto check
if /I "%~1"=="test" goto test
if /I "%~1"=="coverage" goto coverage
if /I "%~1"=="demo" goto demo
if /I "%~1"=="build" goto build
if /I "%~1"=="venv" goto venv
if /I "%~1"=="sync" goto sync
if /I "%~1"=="install-uv" goto install-uv
if /I "%~1"=="clean" goto clean
if /I "%~1"=="help" goto help

echo Unknown command: %~1
goto help

:format
if not exist ".venv\" call make.bat venv
echo Formatting code...
uv run isort src tests tools examples
uv run black -tpy312 src tests tools examples
goto end

:check
if not exist ".venv\" call make.bat venv
echo Running type checks...
uv run pyright src tests tools examples
goto end

:test
if not exist ".venv\" call make.bat venv
echo Running unit tests...
uv run pytest
goto end

:coverage
setlocal
if not exist ".venv\" call make.bat venv
echo Running test coverage...
uv run coverage erase
set "COVERAGE_PROCESS_START=.coveragerc"
uv run coverage run -m pytest
uv run coverage combine
uv run coverage report
endlocal
goto end


:demo
if not exist ".venv\" call make.bat venv
echo Running query tool...
uv run python -m tools.query
goto end

:build
if not exist ".venv\" call make.bat venv
echo Building package...
uv build
goto end

:venv
echo Creating virtual environment...
uv sync -q
uv run python --version
uv run black --version
uv run pyright --version
uv run pytest --version
goto end

:sync
uv sync
goto end

:install-uv
echo Installing uv requires Administrator mode!
echo 1. Using PowerShell in Administrator mode:
echo    Invoke-RestMethod https://astral.sh/uv/install.ps1 ^| Invoke-Expression
echo 2. Add ~/.local/bin to $env:PATH, e.g. by putting
echo        $env:PATH += ";$HOME\.local\bin
echo    in your PowerShell profile ($PROFILE) and restarting PowerShell.
echo    (Sorry, I have no idea how to do that in cmd.exe.)
goto end

:clean
echo Cleaning out build and dev artifacts...
if exist build rmdir /s /q build
if exist dist rmdir /s /q dist
if exist typeagent.egg-info rmdir /s /q typeagent.egg-info
if exist .venv rmdir /s /q .venv
if exist .pytest_cache rmdir /s /q .pytest_cache
goto end

:help
echo Usage: .\make [format^|check^|test^|coverage^|demo^|build^|venv^|sync^|install-uv^|clean^|help]
goto end

:end
